apply plugin: 'com.android.library'
apply from: "../script_config/maven.gradle"
if (rootProject.ext.publish.isEmbedSensorsSDKAAR) { //用于打本地包
    apply plugin: 'com.kezong.fat-aar'
}
version = rootProject.ext.versions.sdk_version

android {
    compileSdkVersion rootProject.ext.buildConfig.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.buildConfig.minSdkVersion
        targetSdkVersion rootProject.ext.buildConfig.targetSdkVersion
        versionCode rootProject.ext.buildConfig.versionCode
        versionName rootProject.ext.buildConfig.versionName
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }

    testOptions {
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                outputs.upToDateWhen { false }
                showStandardStreams = true
            }
        }
        unitTests.includeAndroidResources = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    if (rootProject.ext.publish.isEmbedSensorsSDKAAR) { //用于打本地包
        embed project(':module_common')
        embed project(':module_webview')
        embed project(':module_advert')
        embed project(':module_visual')
        embed project(':module_autoTrack')
        embed project(':module_push')
        embed project(':module_encrypt')
        embed project(':module_exposure')
    } else {
        api project(':module_core')
        api project(':module_advert')
        api project(':module_visual')
        api project(':module_autoTrack')
        api project(':module_push')
        api project(':module_encrypt')
        api project(':module_exposure')
    }
}